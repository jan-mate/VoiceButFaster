buildscript {

  ext.buildConfig = [
      version   : [
          'major': 3,
          'minor': 6,
          'patch': 3,
          'build': 0,
      ],
      compileSdk: 27,
      minSdk    : 21,
      targetSdk : 27
  ]

  def versionSuffix = ""
  ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}$versionSuffix"
  ext.buildConfig.version['code'] = buildConfig.version.major * 1_000_000 + buildConfig.version.minor * 10_000 + buildConfig.version.patch * 100 + buildConfig.version.build

  ext.versions = [
      support            : "27.1.0",
      androidPlugin      : "3.1.0",
      conductor          : "2.1.4",
      dagger             : "2.15",
      exoPlayer          : "2.8.0",
      exoPlayerExtensions: "2.7.0-1",
      coroutines         : "0.22.5",
      kotlin             : "1.2.41",
      room               : "1.1.0-beta1"
  ]

  ext.deps = [
      Support             : [
          supportAnnotations: "com.android.support:support-annotations:${versions.support}",
          appCompat         : "com.android.support:appcompat-v7:${versions.support}",
          constraintLayout  : "com.android.support.constraint:constraint-layout:1.1.0-beta6",
          design            : "com.android.support:design:${versions.support}",
          recyclerView      : "com.android.support:recyclerview-v7:${versions.support}",
          transitions       : "com.android.support:transition:${versions.support}",
          v13               : "com.android.support:support-v13:${versions.support}",
          palette           : "com.android.support:palette-v7:${versions.support}",
          testRunner        : "com.android.support.test:runner:1.0.1"
      ],

      dataBindingCompiler : "com.android.databinding:compiler:${versions.androidPlugin}",
      androidGradlePlugin : "com.android.tools.build:gradle:${versions.androidPlugin}",
      floatingActionButton: "com.getbase:floatingactionbutton:1.10.1",
      materialDialogs     : "com.afollestad.material-dialogs:core:0.9.6.0",
      materialCab         : "com.afollestad:material-cab:0.1.12",
      picasso             : "com.squareup.picasso:picasso:2.5.2",
      tapTarget           : "com.getkeepsafe.taptargetview:taptargetview:1.11.0",
      chapterReader       : "com.github.PaulWoitaschek:ChapterReader:0.1.4",

      Conductor           : [
          base     : "com.bluelinelabs:conductor:${versions.conductor}",
          lifecycle: "com.bluelinelabs:conductor-rxlifecycle2:${versions.conductor}",
          support  : "com.bluelinelabs:conductor-support:${versions.conductor}"
      ],

      Arch                : [
          coreRuntime: "android.arch.core:runtime:1.1.1",
          Room       : [
              runtime : "android.arch.persistence.room:runtime:${versions.room}",
              compiler: "android.arch.persistence.room:compiler:${versions.room}",
              testing : "android.arch.persistence.room:testing:${versions.room}",
              rxJava  : "android.arch.persistence.room:rxjava2:${versions.room}"
          ],
      ],

      crashlytics         : "com.crashlytics.sdk.android:crashlytics:2.9.1@aar",
      fabricGradlePlugin  : "io.fabric.tools:gradle:1.25.2",

      Dagger              : [
          core            : "com.google.dagger:dagger:${versions.dagger}",
          android         : "com.google.dagger:dagger-android-support:${versions.dagger}",
          androidProcessor: "com.google.dagger:dagger-android-processor:${versions.dagger}",
          compiler        : "com.google.dagger:dagger-compiler:${versions.dagger}"
      ],

      ExoPlayer           : [
          core: "com.google.android.exoplayer:exoplayer-core:${versions.exoPlayer}",
          opus: "com.github.PaulWoitaschek.ExoPlayer-Extensions:opus:${versions.exoPlayerExtensions}",
          flac: "com.github.PaulWoitaschek.ExoPlayer-Extensions:flac:${versions.exoPlayerExtensions}"
      ],

      moshi               : "com.squareup.moshi:moshi:1.5.0",
      rxAndroid           : "io.reactivex.rxjava2:rxandroid:2.0.2",
      rxInterop           : "com.github.akarnokd:rxjava2-interop:0.12.5",
      rxJava              : "io.reactivex.rxjava2:rxjava:2.1.12",
      rxPreferences       : "com.f2prateek.rx.preferences:rx-preferences:1.0.2",
      timber              : "com.jakewharton.timber:timber:4.7.0",

      Kotlin              : [
          std              : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
          coroutines       : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}",
          coroutinesAndroid: "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}",
          gradlePlugin     : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
      ],

      junit               : "junit:junit:4.12",
      mockito             : "org.mockito:mockito-core:2.17.0",
      mockitoKotlin       : "com.nhaarman:mockito-kotlin-kt1.1:1.5.0",
      truth               : "com.google.truth:truth:0.40",
      androidKtx          : 'androidx.core:core-ktx:0.2'
  ]

  repositories {
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath deps.androidGradlePlugin
    classpath deps.Kotlin.gradlePlugin
    classpath deps.fabricGradlePlugin
  }
}

plugins {
  id 'com.gradle.build-scan' version '1.13'
  id 'com.github.ben-manes.versions' version '0.17.0'
}

buildScan { licenseAgreementUrl = 'https://gradle.com/terms-of-service'; licenseAgree = 'yes' }

allprojects {
  repositories {
    jcenter()
    maven { url "https://maven.google.com" }
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
  }

  dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
      rules.all { ComponentSelection selection ->
        def candidate = selection.candidate
        if (['rc', 'beta', 'alpha'].any { forbidden -> candidate.version.contains(forbidden) }) selection.reject("blacklisted")
        if (candidate.group == 'javax.annotation' && candidate.version == '1.0-20050927.133100') selection.reject()
      }
    }
  }
  configurations.all {
    resolutionStrategy {
      force deps.Support.v13
      force deps.Support.supportAnnotations
      force deps.Kotlin.std
      force 'com.google.code.findbugs:jsr305:3.0.1'
      force deps.Arch.coreRuntime
    }
  }
}

subprojects {
  // workaround for https://github.com/Takhion/kotlin-metadata/issues/8
  configurations.all {
    resolutionStrategy {
      eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'kotlin-compiler-embeddable') {
          details.useVersion "1.2.31"
        }
      }
    }
  }
}

apply from: "predexDisabler.gradle"

task importStrings(type: Exec) {
  executable 'sh'
  args "-c", "tx pull -af --minimum-perc=5"
}
