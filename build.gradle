import deps.Deps
import deps.Versions

buildscript {

  repositories {
    maven { url "https://maven.google.com" }
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
  }

  dependencies {
    classpath Deps.androidGradlePlugin
    classpath Deps.Kotlin.gradlePlugin
    classpath Deps.fabricGradlePlugin
  }
}

plugins {
  id 'com.gradle.build-scan' version '1.16'
  id 'com.github.ben-manes.versions' version '0.20.0'
}

buildScan { licenseAgreementUrl = 'https://gradle.com/terms-of-service'; licenseAgree = 'yes' }

task appVersion {
  print '#BEGIN_VERSION#' + Versions.versionName + '_' + Versions.versionCode + '#END_VERSION#'
}

allprojects {
  repositories {
    maven { url "https://maven.google.com" }
    google()
    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
  }

  dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
      rules.all { ComponentSelection selection ->
        def candidate = selection.candidate
        if (['rc', 'beta', 'alpha'].any { forbidden -> candidate.version.toLowerCase().contains(forbidden) }) selection.reject("blacklisted")
        if (candidate.group == 'javax.annotation' && candidate.version == '1.0-20050927.133100') selection.reject()
      }
    }
  }
  configurations.all {
    resolutionStrategy {
      force Deps.AndroidX.supportAnnotations
      force Deps.Kotlin.std
      force 'com.google.code.findbugs:jsr305:3.0.1'
    }
  }
}

apply from: "predexDisabler.gradle"

task importStrings(type: Exec) {
  executable 'sh'
  args "-c", "tx pull -af --minimum-perc=5"
}
